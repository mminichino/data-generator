plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.github.node-gradle.node' version '7.1.0'
}

import org.gradle.api.tasks.options.Option

group = 'com.codelry.util'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.apache.commons:commons-pool2'
    implementation 'io.lettuce:lettuce-core:6.8.1.RELEASE'
    implementation 'com.redis:lettucemod:4.5.0'
    implementation 'io.micrometer:micrometer-core'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.78.1'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.xerial:sqlite-jdbc:3.51.0.0'
    implementation 'com.hubspot.jinjava:jinjava:2.8.2'
    implementation 'org.apache.commons:commons-csv:1.14.1'
    implementation 'org.apache.commons:commons-lang3:3.19.0'
    implementation 'dev.langchain4j:langchain4j:1.8.0'
    implementation 'dev.langchain4j:langchain4j-open-ai:1.8.0'
    implementation 'commons-cli:commons-cli:1.11.0'
    implementation 'org.apache.commons:commons-text:1.14.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation platform('org.testcontainers:testcontainers-bom:1.21.3')
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers'
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

tasks.test {
    enabled = true
}

tasks.register('runDev', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['start', 'npm', '--name', 'data-generator', '--', 'run', 'dev']
}

tasks.register('startDev', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['start', 'data-generator']
}

tasks.register('restartDev', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['restart', 'data-generator']
}

tasks.register('stopDev', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['stop', 'data-generator']
}

tasks.register('deleteDev', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['delete', 'data-generator']
}

tasks.register('pmList', NpxTask) {
    dependsOn npmInstall
    command = 'pm2'
    args = ['list']
}

abstract class CreateDatabaseTask extends JavaExec {
    @Option(option = 'names', description = 'Generate Names')
    void setNames(boolean names) {
        if (names) args '--names'
    }

    @Option(option = 'address', description = 'Generate Addresses')
    void setAddress(boolean address) {
        if (address) args '--address'
    }

    @Option(option = 'phone', description = 'Phones CSV file path')
    void setPhone(String phone) {
        args '--phone', phone
    }

    @Option(option = 'zip', description = 'Zip Codes CSV file path')
    void setZip(String zip) {
        args '--zip', zip
    }

    @Option(option = 'state', description = 'State Data CSV file path')
    void setState(String state) {
        args '--state', state
    }

    @Option(option = 'products', description = 'Generate Products')
    void setProducts(boolean products) {
        if (products) args '--products'
    }
}

tasks.register('createDatabase', CreateDatabaseTask) {
    group = 'application'
    description = 'Runs the database generation utility'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.codelry.util.generator.internal.CreateDatabase'
}
